/'*******************************************************************************************
*
*   raylib [textures] example - Load textures from raw data
*
*   NOTE: Images are loaded in CPU memory (RAM); textures are loaded in GPU memory (VRAM)
*
*   This example has been created using raylib 1.3 (www.raylib.com)
*   raylib is licensed under an unmodified zlib/libpng license (View raylib.h for details)
*
*   Copyright (c) 2015 Ramon Santamaria (@raysan5)
*
********************************************************************************************'/

#include once "../raylib.bi"

'' Initialization
const as long _
  screenWidth = 800, screenHeight = 450

InitWindow( screenWidth, screenHeight, "raylib [textures] example - texture from raw data" )

'' Load RAW image data (512x512, 32bit RGBA, no file header)
dim as Image fudesumiRaw = LoadImageRaw( "resources/fudesumi.raw", 384, 512, UNCOMPRESSED_R8G8B8A8, 0 )
dim as Texture2D fudesumi = LoadTextureFromImage( fudesumiRaw )
UnloadImage( fudesumiRaw )

'' Generate a checked texture by code
dim as long txWidth = 960, txHeight = 480

'' Dynamic memory allocation to store pixels data (Color type)
dim as RayColor ptr pixels = MemAlloc( txWidth * txHeight * sizeof( RayColor ) )

for y as integer = 0 to txHeight - 1
  for x as integer = 0 to txWidth - 1
    if( ( ( x / 32 + y / 32 ) / 1 ) mod 2 = 0 ) then
      pixels[ y * txWidth + x ] = ORANGE
    else
      pixels[ y * txWidth + x ] = GOLD
    end if
  next
next

'' Load pixels data into an image structure and create texture
dim as Image checkedIm

with checkedIm
  .data = pixels '' We can assign pixels directly to data
  .width = txWidth
  .height = txHeight
  .format = UNCOMPRESSED_R8G8B8A8
  .mipmaps = 1
end with

dim as Texture2D checked = LoadTextureFromImage( checkedIm )
UnloadImage( checkedIm )

'' Main game loop
do while( not WindowShouldClose() )
  '' Update
  ''----------------------------------------------------------------------------------
  '' TODO: Update your variables here
  ''----------------------------------------------------------------------------------

  '' Draw
  BeginDrawing()
    ClearBackground( RAYWHITE )
    
    DrawTexture( checked, screenWidth / 2 - checked.width / 2, screenHeight / 2 - checked.height / 2, Fade( WHITE, 0.5f ) )
    DrawTexture( fudesumi, 430, -30, WHITE )
    
    DrawText( "CHECKED TEXTURE ", 84, 85, 30, BROWN )
    DrawText( "GENERATED by CODE", 72, 148, 30, BROWN )
    DrawText( "and RAW IMAGE LOADING", 46, 210, 30, BROWN )
    
    DrawText( "(c) Fudesumi sprite by Eiden Marsal", 310, screenHeight - 20, 10, BROWN )
  EndDrawing()
loop

'' De-Initialization
UnloadTexture( fudesumi )
UnloadTexture( checked )

CloseWindow()
